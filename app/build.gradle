apply plugin: 'com.android.application'

project.ext {
    androidSupportLibsVersion = "27.1.1"
    androidConstraintLayoutVersion = "1.1.2"
    rxAndroidVersion = "2.0.2"
    rxJavaVersion = "2.1.17"
    butterknifeVersion = "8.8.1"
    junitVersion = "4.12"
    androidTestRunnerVersion = "1.0.2"
    androidEspressoVersion = "3.0.2"
    daggerVersion = "2.16"
    commonsLangVersion = "3.7"
    archVersion = "1.1.1"
}

android {
    compileSdkVersion 27
    buildToolsVersion "28.0.1"
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    defaultConfig {
        applicationId "hu.psprog.leaflet.mobile"
        minSdkVersion 24
        targetSdkVersion 27
        versionCode 1
        versionName "0.1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {

    // standard android dependencies
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:$androidSupportLibsVersion"
    implementation "com.android.support:support-v4:$androidSupportLibsVersion"
    implementation "com.android.support:design:$androidSupportLibsVersion"
    implementation "com.android.support.constraint:constraint-layout:$androidConstraintLayoutVersion"
    implementation "android.arch.lifecycle:extensions:$archVersion"
    implementation "android.arch.lifecycle:viewmodel:$archVersion"

    // external dependencies
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    implementation "com.jakewharton:butterknife:$butterknifeVersion"
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    implementation "org.apache.commons:commons-lang3:$commonsLangVersion"

    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-android-processor:$daggerVersion"

    // test dependencies
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "com.android.support.test:runner:$androidTestRunnerVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$androidEspressoVersion"
}
